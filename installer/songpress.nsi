; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Songpress"
!define PRODUCT_VERSION "(net installer)"
!define PRODUCT_PUBLISHER "Luca Allulli - Skeed"
!define PRODUCT_WEB_SITE "http://www.skeed.it"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\songpress.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

RequestExecutionLevel admin
SetCompressor lzma

Function UninstallOld
  ; $R0 should contain the GUID of the application, i.e. Songpress
  push $R1
  ReadRegStr $R1 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\$R0" "UninstallString"
  StrCmp $R1 "" UninstallMSI_nomsi
    MessageBox MB_YESNOCANCEL|MB_ICONQUESTION  $(UninstallAsk) IDNO UninstallMSI_nomsi IDYES UninstallMSI_yesmsi
      Abort
UninstallMSI_yesmsi:
    ExecWait $R1
    MessageBox MB_OK|MB_ICONINFORMATION $(UninstallPressOk)
UninstallMSI_nomsi:
  pop $R1
FunctionEnd

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "FileAssociation.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\license.txt"
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Songpress"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Components
!insertmacro MUI_PAGE_COMPONENTS
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\songpress.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Italian"

; --- English Strings ---
LangString UninstallAsk ${LANG_ENGLISH} "A previous version of Songpress was found. It is recommended that you uninstall it first. Do you want to do that now?"
LangString UninstallPressOk ${LANG_ENGLISH} "Press OK to continue upgrading your version of Songpress"
LangString STR_INSTALLING_SONGPRESS ${LANG_ENGLISH} "Installing Songpress via uv..."
LangString STR_INSTALL_SUCCESS ${LANG_ENGLISH} "Installation completed successfully."
LangString STR_FALLBACK_PS ${LANG_ENGLISH} "Attempting fallback via PowerShell..."
LangString STR_CRITICAL_ERROR ${LANG_ENGLISH} "Critical error: Songpress not installed."

; --- Italian Strings ---
LangString UninstallAsk ${LANG_ITALIAN} "E' presente una versione precedente di Songpress. Si consiglia di disinstallarla prima di continuare. Eseguire la disinstallazione ora?"
LangString UninstallPressOk ${LANG_ITALIAN} "Premi OK per continuare l'aggiornamento di Songpress"
LangString STR_INSTALLING_SONGPRESS ${LANG_ITALIAN} "Installazione Songpress in corso con uv..."
LangString STR_INSTALL_SUCCESS ${LANG_ITALIAN} "Installazione completata con successo."
LangString STR_FALLBACK_PS ${LANG_ITALIAN} "Tentativo di fallback tramite PowerShell..."
LangString STR_CRITICAL_ERROR ${LANG_ITALIAN} "Errore critico: Songpress non installato."

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "songpress-${PRODUCT_VERSION}-setup.exe"
InstallDir "$PROGRAMFILES\Songpress"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
AutoCloseWindow false

; Global variables
Var SongpressExe
Var UvCmd

; Installing UV + Python + Songpress

Function DoInstallPythonAndSongpress
  ; 1. Preparing uv
  InitPluginsDir
  File "/oname=$PLUGINSDIR\uv.exe" "uv.exe"
  StrCpy $UvCmd "$PLUGINSDIR\uv.exe"

  ; 2. Variable Setup
  ; Use SetEnvironmentVariable for the current session
  System::Call 'kernel32::SetEnvironmentVariable(t "UV_TOOL_BIN_DIR", t "$INSTDIR\bin")'
  System::Call 'kernel32::SetEnvironmentVariable(t "UV_TOOL_DIR", t "$INSTDIR\tools")'
  System::Call 'kernel32::SetEnvironmentVariable(t "UV_PYTHON_INSTALL_DIR", t "$INSTDIR\python")'
  System::Call 'kernel32::SetEnvironmentVariable(t "UV_CACHE_DIR", t "$INSTDIR\cache")'

  ; 3. EXECUTE SONGPRESS INSTALLATION
  DetailPrint "$(STR_INSTALLING_SONGPRESS)"

  ; Use the full path of uv.exe in quotes
  ; Add --force to ensure it overwrites any failed attempts
  nsExec::ExecToLog '"$UvCmd" tool install --force --python-preference system songpress'

  ; 4. FINAL VERIFICATION AND FALLBACK
  ${If} ${FileExists} "$INSTDIR\bin\songpress.exe"
    DetailPrint "$(STR_INSTALL_SUCCESS)"
  ${Else}
    ; If it still fails, try calling it via PowerShell as a last resort
    DetailPrint "$(STR_FALLBACK_PS)"
    nsExec::ExecToLog 'powershell -Command "& { \
      $$env:UV_TOOL_BIN_DIR=$\"$INSTDIR\bin$\"; \
      $$env:UV_TOOL_DIR=$\"$INSTDIR\tools$\"; \
      $$env:UV_PYTHON_INSTALL_DIR=$\"$INSTDIR\python$\"; \
      $$env:UV_CACHE_DIR=$\"$INSTDIR\cache$\"; \
      & $\"$UvCmd$\" tool install --force songpress \
    }"'
    ${Unless} ${FileExists} "$INSTDIR\bin\songpress.exe"
       DetailPrint "$(STR_CRITICAL_ERROR)"
       Abort
    ${EndUnless}
  ${EndIf}

  StrCpy $SongpressExe "$INSTDIR\bin\songpress.exe"

  RMDir /r "$INSTDIR\cache"
FunctionEnd

; Uninstall Songpress
Function un.DoUninstallSongpress
  RMDir /r "$INSTDIR\bin"
  RMDir /r "$INSTDIR\tools"
  RMDir /r "$INSTDIR\python"
  RMDir /r "$INSTDIR\cache"
  Delete "$INSTDIR\songpress.ico"
  Delete "$INSTDIR\uninst.exe"
  RMDir "$INSTDIR"
FunctionEnd

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

LangString SongpressSectionNameLS ${LANG_ENGLISH} "Songpress"
LangString SongpressSectionNameLS ${LANG_ITALIAN} "Songpress"
LangString DesktopSectionNameLS ${LANG_ENGLISH} "Desktop shortcut"
LangString DesktopSectionNameLS ${LANG_ITALIAN} "Icona sul Desktop"
LangString CrdSectionNameLS ${LANG_ENGLISH} "Associate CRD files"
LangString CrdSectionNameLS ${LANG_ITALIAN} "Associa i file CRD"
LangString ChoSectionNameLS ${LANG_ENGLISH} "Associate CHO files"
LangString ChoSectionNameLS ${LANG_ITALIAN} "Associa i file CHO"
LangString ChordproSectionNameLS ${LANG_ENGLISH} "Associate Chordpro files"
LangString ChordproSectionNameLS ${LANG_ITALIAN} "Associa i file Chordpro"
LangString ChoproSectionNameLS ${LANG_ENGLISH} "Associate Chopro files"
LangString ChoproSectionNameLS ${LANG_ITALIAN} "Associa i file Chopro"
LangString ProSectionNameLS ${LANG_ENGLISH} "Associate PRO files"
LangString ProSectionNameLS ${LANG_ITALIAN} "Associa i file PRO"
LangString TabSectionNameLS ${LANG_ENGLISH} "Associate TAB files"
LangString TabSectionNameLS ${LANG_ITALIAN} "Associa i file TAB"
LangString FileAssociationSG ${LANG_ENGLISH} "File type association"
LangString FileAssociationSG ${LANG_ITALIAN} "Associa tipi di file"

;Section "Visual Studio Runtime"
;  SetOutPath "$INSTDIR"
;  SetOverwrite on
;  File "installer\VC_redist.x86.exe"
;  ExecWait "$INSTDIR\VC_redist.x86.exe /install /passive /norestart"
;  Delete "$INSTDIR\VC_redist.x86.exe"
;SectionEnd

Section $(SongpressSectionNameLS) SongpressSection

  push $R0
    StrCpy $R0 "Songpress";  the ProductID of my package
    Call UninstallOld
  pop $R0

  SetShellVarContext all
  SectionIn RO
  ; SetOutPath "$INSTDIR"
  ; SetOverwrite on
  Call DoInstallPythonAndSongpress
  SetOutPath "$INSTDIR"
  File songpress.ico

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Songpress.lnk" "$INSTDIR\bin\songpress.exe" "" "$INSTDIR\songpress.ico" 0
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section $(DesktopSectionNameLS) DesktopSection
  ; SetShellVarContext all
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateShortCut "$DESKTOP\Songpress.lnk" "$INSTDIR\bin\songpress.exe" "" "$INSTDIR\songpress.ico"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -AdditionalIcons
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  nsExec::Exec "del $SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

SectionGroup $(FileAssociationSG)
Section $(CrdSectionNameLS) CrdSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".crd" "ChordPro"
  WriteRegStr HKCU "Software\Classes\ChordPro\DefaultIcon" "" "$INSTDIR\songpress.ico"
SectionEnd
Section $(ChoSectionNameLS) ChoSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".cho" "ChordPro"
  WriteRegStr HKCU "Software\Classes\ChordPro\DefaultIcon" "" "$INSTDIR\songpress.ico"
SectionEnd
Section $(ChordproSectionNameLS) ChordproSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".chordpro" "ChordPro"
  WriteRegStr HKCU "Software\Classes\ChordPro\DefaultIcon" "" "$INSTDIR\songpress.ico"
SectionEnd
Section $(ChoproSectionNameLS) ChoproSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".chopro" "ChordPro"
  WriteRegStr HKCU "Software\Classes\ChordPro\DefaultIcon" "" "$INSTDIR\songpress.ico"
SectionEnd
Section $(TabSectionNameLS) TabSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".tab" "TABChord"
  WriteRegStr HKCU "Software\Classes\TABChord\DefaultIcon" "" "$INSTDIR\songpress.ico"
SectionEnd
Section $(ProSectionNameLS) ProSection
  ${registerExtension} "$INSTDIR\bin\songpress.exe" ".pro" "PROChord"
  WriteRegStr HKCU "Software\Classes\PROChord\DefaultIcon" "" "$INSTDIR\songpress.ico"

  ;Notify Explorer about changes
  System::Call 'shell32::SHChangeNotify(i 0x08000000, i 0, i 0, i 0)'
SectionEnd
SectionGroupEnd


Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\songpress.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\songpress.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


;--------------------------------
;Descriptions

  ;USE A LANGUAGE STRING IF YOU WANT YOUR DESCRIPTIONS TO BE LANGAUGE SPECIFIC

  ;Assign descriptions to sections
  LangString SongpressSectionLS ${LANG_ENGLISH} "Songpress program files"
  LangString SongpressSectionLS ${LANG_ITALIAN} "File eseguibili di Songpress"
  LangString DesktopSectionLS ${LANG_ENGLISH} "Create a shortcut on the Desktop"
  LangString DesktopSectionLS ${LANG_ITALIAN} "Crea un'icona di Songpress sul Desktop"
  LangString CrdSectionLS ${LANG_ENGLISH} "Open .crd files with Songpress"
  LangString CrdSectionLS ${LANG_ITALIAN} "Apre i file .crd con Songpress"
  LangString ChoSectionLS ${LANG_ENGLISH} "Open .cho files with Songpress"
  LangString ChoSectionLS ${LANG_ITALIAN} "Apre i file .cho con Songpress"
  LangString ChordproSectionLS ${LANG_ENGLISH} "Open .chordpro files with Songpress"
  LangString ChordproSectionLS ${LANG_ITALIAN} "Apre i file .chordpro con Songpress"
  LangString ChoproSectionLS ${LANG_ENGLISH} "Open .chopro files with Songpress"
  LangString ChoproSectionLS ${LANG_ITALIAN} "Apre i file .chopro con Songpress"
  LangString TabSectionLS ${LANG_ENGLISH} "Open .tab files with Songpress"
  LangString TabSectionLS ${LANG_ITALIAN} "Apre i file .tab con Songpress"
  LangString ProSectionLS ${LANG_ENGLISH} "Open .pro files with Songpress"
  LangString ProSectionLS ${LANG_ITALIAN} "Apre i file .pro con Songpress"

  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SongpressSection} $(SongpressSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${DesktopSection} $(DesktopSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${CrdSection} $(CrdSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${ChoSection} $(ChoSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${ChordproSection} $(ChordproSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${ChoproSection} $(ChoproSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${TabSection} $(TabSectionLS)
    !insertmacro MUI_DESCRIPTION_TEXT ${ProSection} $(ProSectionLS)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

LangString UninstallComplete ${LANG_ENGLISH} "$(^Name) has been successfully removed from your computer."
LangString UninstallComplete ${LANG_ITALIAN} "$(^Name) e' stato disinstallato con successo."

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK $(UninstallComplete)
FunctionEnd

LangString UninstallConfirm ${LANG_ENGLISH} "Are you sure to completely remove $(^Name)?"
LangString UninstallConfirm ${LANG_ITALIAN} "Sei sicuro di voler rimuovere $(^Name)?"

Function un.onInit
  !insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 $(UninstallConfirm) IDYES +2
  Abort
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  ; SetShellVarContext all
  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$DESKTOP\Songpress.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Songpress.lnk"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  Delete "$INSTDIR\uninst.exe"

  Call un.DoUninstallSongpress

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  ${unregisterExtension} ".crd" "ChordPro"
  ${unregisterExtension} ".cho" "ChordPro"
  ${unregisterExtension} ".chordpro" "ChordPro"
  ${unregisterExtension} ".chopro" "ChordPro"
  ${unregisterExtension} ".tab" "TABChord"
  ${unregisterExtension} ".tab" "PROChord"

  ;Notify Explorer about changes
  System::Call 'shell32::SHChangeNotify(i 0x08000000, i 0, i 0, i 0)'

  SetAutoClose true
SectionEnd